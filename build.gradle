plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'dsko'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


dependencies {
    //스프링 웹
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //스프링 시큐리티
    implementation 'org.springframework.boot:spring-boot-starter-security'
    //Oatuh
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    //jwt
    implementation("io.jsonwebtoken:jjwt-api:0.12.3")
    implementation("io.jsonwebtoken:jjwt-impl:0.12.3")
    implementation("io.jsonwebtoken:jjwt-jackson:0.12.3")
    implementation 'com.nimbusds:nimbus-jose-jwt:9.37'

    //프로젝트 롬복
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //데이타베이스 관련 설정
    runtimeOnly 'com.h2database:h2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    //Spring Data Querydsl
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    //p6spy - SQL 쿼리 로깅(value가 상세히 보임)
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'
    //Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.session:spring-session-data-redis'

    //테스트 관련 설정
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //Swagger
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0")
}
tasks.named('test') {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

//------------------ QueryDSL 설정 ------------------

def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory.set(querydslDir)
}

sourceSets {
    main.java.srcDirs += [querydslDir]
}

clean.doLast {
    file(querydslDir).deleteDir()
}

//-----------JaCoCo 설정 ----------------

jacoco {
    toolVersion = "0.8.13" // https://www.jacoco.org/jacoco/trunk/doc/changes.html
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
    }

    def QDomains = []
    for (qPattern in '**/QA'..'**/QZ') {
        QDomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            '**/*Application.class',
                            '**/*Config.class',
                            '**/*Dto.class',
                            '**/*Request.class',
                            '**/*Response.class',
                            '**/*Interceptor.class',
                            '**/*Exception.class',
                            '**/domain/**', // domain 디렉토리 하위의 모든 클래스 제외
                            '**/dto/**', // dto 디렉토리 하위의 모든 클래스 제외
                            '**.*config.*', // 모든 config 패키지 제외
                            '**.*exception.*' // 모든 exception 패키지 제외
                    ] + QDomains)
                })
        )
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    def QDomains = []
    for (qPattern in '*.QA'..'*.QZ') {
        QDomains.add(qPattern + '*')
    }

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            excludes = [
                    '**.*Application*',
                    '**.*Config*',
                    '**.*Dto*',
                    '**.*Request*',
                    '**.*Response*',
                    '**.*Interceptor*',
                    '**.*Exception*',
                    '**.*domain.*', // 모든 domain 패키지 제외
                    '**.*dto.*', // 모든 dto 패키지 제외
                    '**.*config.*', // 모든 config 패키지 제외
                    '**.*exception.*' // 모든 exception 패키지 제외
            ] + QDomains
        }
    }
}